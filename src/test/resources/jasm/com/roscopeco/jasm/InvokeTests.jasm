public class com/roscopeco/jasm/InvokeTests {
    public <init>()V {
        aload 0
        invokespecial java/lang/Object.<init>()V
        return
    }

    private static staticMethod() java/lang/String {
        ldc "Hello World"
        areturn
    }

    private addStrToList(java/util/List) java/util/List {
        aload 1  // TODO use DUP when implemented!
        invokestatic com/roscopeco/jasm/InvokeTests.staticMethod()java/lang/String

        invokeinterface java/util/List.add(java/lang/Object)Z

        aload 1
        areturn
    }

    public testMethod(java/util/List) java/util/List {
        aload 0
        aload 1
        invokevirtual com/roscopeco/jasm/InvokeTests.addStrToList(java/util/List)java/util/List

        aload 1
        areturn
    }

    public testArrayReceiver(java/lang/Object) java/lang/Object {
        aload 1
        checkcast [java/lang/Object
        invokevirtual [java/lang/Object.clone()java/lang/Object
        areturn
    }

    public testStaticOnInterface() java/util/Map {
        ldc "World"
        bipush 97
        invokestatic java/lang/Character.valueOf(C)java/lang/Character

        // asterisk here forces an InterfaceMethodref to be generated, needed for e.g. statics on interfaces.
        invokestatic* java/util/Map.of(java/lang/Object, java/lang/Object)java/util/Map
        areturn
    }
}