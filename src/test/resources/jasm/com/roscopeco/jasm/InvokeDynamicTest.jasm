// This class is both a test and example of how to use invokedynamic!
//
public class com/roscopeco/jasm/InvokeDynamic implements com/roscopeco/jasm/model/InvokedynamicTest {
    public <init>()V {
        aload 0
        invokespecial java/lang/Object.<init>()V
        return
    }

    // This is a basic test of invokedynamic, using the LambdaMetafactory as the bootstrap method...
    //
    // See below for a more complete test, this is really just here as an example.
    //
    // This is broadly similar to what javac would do if you passed
    // () -> "some string" as a Supplier<String> parameter.
    //
    public doBasicInvokeDynamicTest()java/lang/String {
        invokedynamic get()java/util/function/Supplier {
            invokestatic java/lang/invoke/LambdaMetafactory.metafactory(
                java/lang/invoke/MethodHandles$Lookup,
                java/lang/String,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodHandle,
                java/lang/invoke/MethodType,
            )java/lang/invoke/CallSite
            [
                ()java/lang/Object,
                invokestatic com/roscopeco/jasm/model/TestBootstrap.lambdaGetImpl()java/lang/String,
                ()java/lang/String
            ]
        }

        invokeinterface java/util/function/Supplier.get()java/lang/Object
        checkcast java/lang/String
        areturn
    }

    // This is testing all the features of invokedynamic, so is a bit convoluted. See also
    // com.roscopeco.jasm.model.TestBootstrap#testBootstrap for where this gets linked.
    //
    // * Calls a dynamic method 'get' with no arguments that returns Object
    // * Linkage is done via com.roscopeco.jasm.model.TestBootstrap#testBootstrap
    // * This has seven additional arguments (additional to the first three, which are stacked by the JVM automatically)
    //   * An int (42, passed statically here)
    //   * A float (10.0, passed statically here)
    //   * A String ("Bootstrap test", passed statically here)
    //   * A Class (java.util.List, passed statically here)
    //   * A MethodHandle (an invokestatic of TestBootstrap#staticForHandleTest, passed statically here)
    //   * A MethodType ( (java/lang/String;)I, passed statically here)
    //   * Another String, passed via a CONSTDYNAMIC lookup using java.lang.invoke.ConstantBootstraps#getStaticFinal
    //     * The value for this comes from the DYNAMIC_CONST_FOR_TEST field on TestBootstrap
    //     * The JVM does a static call to ConstantBootstraps#getStaticFinal to actually get the value
    //     * This gets an additional parameter with TestBootstrap as the declaring class, passed statically here
    // * The testBootstrap method creates an instance of TestBootstrap with the supplied dynamic const
    // * And return a ConstCallSite for the TestBootstrap.getImpl method, bound to that instance
    // * getImpl returns the String from the instance (which came from the dynamic const).
    //
    public doInvokeDynamicTest()java/lang/Object {
        invokedynamic get()java/lang/Object {
            invokestatic com/roscopeco/jasm/model/TestBootstrap.testBootstrap(
                java/lang/invoke/MethodHandles$Lookup,
                java/lang/String,
                java/lang/invoke/MethodType,
                I,
                F,
                java/lang/String,
                java/lang/Class,
                java/lang/invoke/MethodHandle,
                java/lang/invoke/MethodType,
                java/lang/String,
            )java/lang/invoke/CallSite
            [
                42,                                                                                             // Static int
                10.0,                                                                                           // Static float
                "Bootstrap test",                                                                               // Static string
                java/util/List,                                                                                 // Static class
                invokestatic com/roscopeco/jasm/model/TestBootstrap.staticForHandleTest()java/lang/String,      // Static MethodHandle
                (java/lang/String)I,                                                                            // Static MethodType
                constdynamic DYNAMIC_CONST_FOR_TEST java/lang/String {                                          // Dynamic const
                    invokestatic java/lang/invoke/ConstantBootstraps.getStaticFinal(
                        java/lang/invoke/MethodHandles$Lookup,
                        java/lang/String,
                        java/lang/Class,
                        java/lang/Class
                    )java/lang/Object
                    [com/roscopeco/jasm/model/TestBootstrap]
                }
            ]
        }

        areturn
    }
}